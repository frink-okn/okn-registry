#+ summary: "Find all diseases in NDE and the Mondo disease parent terms."
#+ tags:
#+   - federation

PREFIX schema: <http://schema.org/>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

SELECT
  ?disease
  (GROUP_CONCAT(
     DISTINCT REPLACE(STR(?parent),
       "^http://purl\\.obolibrary\\.org/obo/MONDO_", "MONDO:");
     separator=" | "
   ) AS ?directMondoParents)
WHERE {
  {
    SELECT DISTINCT ?disease
    WHERE {
      ?dataset a schema:Dataset ;
               schema:healthCondition ?disease .
      ?disease rdfs:isDefinedBy obo:mondo.owl .
    }
    LIMIT 200
  }

  OPTIONAL {
    ?disease rdfs:subClassOf ?parent .
    ?parent rdfs:isDefinedBy obo:mondo.owl .
  }
}
GROUP BY ?disease
LIMIT 1000
